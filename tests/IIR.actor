@sdf
actor add int in1, int in2 ==> int out :
  action in1:[i], in2:[j] ==> out:[i+j] end
end

@sdf
actor delay(int k) int in ==> int out :
  initialize ==> out:[k] end
  action in:[i] ==> out:[i] end
end

@sdf
actor mulc(int c) int in ==> int out :
  action in:[i] ==> out:[c*i] end
end

@sdf
actor rshift(int s) int in ==> int out :
  action in:[i] ==> out:[i >> s] end
end

network iir() int in ==> int out :
  action in:[i] ==> out:[(171*prev(g)+85*i) >> 8] end
  
  invariant tokens(f,1)
  invariant next(f) = 171*prev(g)
  
  chinvariant 171*g[-1] == f[0] 
  
  entities
    del1 = delay(0);
    mul1 = mulc(85);
    mul2 = mulc(171);
    rsh1 = rshift(8);
    add1 = add(); 
  end
  
  structure
    a: in --> mul1.in;
    b: mul1.out --> add1.in1;
    c: add1.out --> rsh1.in;
    d: rsh1.out --> mul2.in;
    e: mul2.out --> del1.in;
    f: del1.out --> add1.in2;
    g: rsh1.out --> out;
  end
end