actor add int in1, int in2 ==> int out :
  action in1:[i], in2:[j] ==> out:[i+j] end
end

actor delay(int k) int in ==> int out :
  initialize ==> out:[k] end
  action in:[i] ==> out:[i] end
end

actor mulc(int c) int in ==> int out :
  action in:[i] ==> out:[c*i] end
end

actor rshift(int s) int in ==> int out :
  action in:[i] ==> out:[i >> s] end
end

actor split int in ==> int out1, int out2 :
  action in:[i] ==> out1:[i], out2:[i] end
end

network iir() int in ==> int out :
  
  action in:1 ==> out:1
    ensures out[0] = ((85*in[0]) >> 8)
    ensures 0 < @(out) ==> out[@] = ((171*out[@-1]+85*in[@]) >> 8)
  end
  
  invariant tokens(f,1)
  
  chinvariant f[0] = 0
  chinvariant 0 < @(h) ==> 171*h[@-1] = f[@] 
  
  entities
    del1 = delay(0);
    mul1 = mulc(85);
    mul2 = mulc(171);
    rsh1 = rshift(8);
    add1 = add();
    spl1 = split();
  end
  
  structure
    a: in --> mul1.in;
    b: mul1.out --> add1.in1;
    c: add1.out --> rsh1.in;
    d: spl1.out1 --> mul2.in;
    e: mul2.out --> del1.in;
    f: del1.out --> add1.in2;
    g: rsh1.out --> spl1.in;
    h: spl1.out2 --> out;
  end
end