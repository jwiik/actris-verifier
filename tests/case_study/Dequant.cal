/* 
BEGINCOPYRIGHT X
	
	Copyright (c) 2004-2005, Xilinx Inc.
	All rights reserved.
	
	Redistribution and use in source and binary forms, 
	with or without modification, are permitted provided 
	that the following conditions are met:
	- Redistributions of source code must retain the above 
	  copyright notice, this list of conditions and the 
	  following disclaimer.
	- Redistributions in binary form must reproduce the 
	  above copyright notice, this list of conditions and 
	  the following disclaimer in the documentation and/or 
	  other materials provided with the distribution.
	- Neither the name of the copyright holder nor the names 
	  of its contributors may be used to endorse or promote 
	  products derived from this software without specific 
	  prior written permission.
	
	THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND 
	CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, 
	INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF 
	MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE 
	DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR 
	CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, 
	SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT 
	NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; 
	LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) 
	HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN 
	CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR 
	OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS 
	SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
	
ENDCOPYRIGHT
*/
// dequant.cal
//
// Author: David B. Parlour (dave.parlour@xilinx.com)
//
// MPEG-4 inverse quantization function


actor Dequant()
	int(size=13) DC, int(size=13) AC, int(size=6) QP
		==>
	int(size=13) OUT :
	
	contract QP:1, DC:1, AC:63 ==> OUT:64 end
	
	invariant tot@(OUT) = rd@(DC) + rd@(AC)
	invariant state(start) ==> ( 
		(rd@(QP) = 0 && rd@(DC) = 0 && rd@(AC) = 0 && tot@(OUT) = 0) || 
		(rd@(QP) = 1 && rd@(DC) = 1 && rd@(AC) = 63 && tot@(OUT) = 64) )
	invariant state(ac) ==> rd@(DC) = 1 && rd@(QP) = 1 && rd@(AC) = bv2int(count)
	
	function saturate(int(size=13) x) --> int(size=13):
		if x < -int(2048,13) then
			-int(2048,13)
		else
			if x > int(2047,13) then
				int(2047,13)
			else
				x
			end
		end
	end

	int(size=7) count;

	int(size=6) quant;

	int(size=2) round;

	get_qp: action QP:[ q ], DC:[ i ] ==> OUT:[ i ]
	do
		quant := q;
		round := bvresize((q & int(1,6)) ^ int(1,6), 2);
		count := int(0,7);
	end

	done: action ==>
	guard
		count = int(63,7)
	end

	function abs(int(size=13) x) --> int(size=13) :
		if x < int(0,13) then
			-x
		else
			x
		end
	end

	ac: action AC:[ i ] ==> OUT:[ saturate(o) ]
	var
		int(size=13) v = bvresize(quant,13) * ((abs(i) << int(1,13)) + int(1,13)) - bvresize(round,13),
		int(size=13) o =
		if i = int(0,13) then
			int(0,13)
		else
			if i < int(0,13) then
				-v
			else
				v
			end
		end
	do
		count := count + int(1,7);
	end

	schedule fsm start :
		start (get_qp) --> ac;
		ac (done) --> start;
		ac (ac) --> ac;
	end

	priority
		done > ac;
	end

end