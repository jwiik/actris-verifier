/* 
BEGINCOPYRIGHT X
	
	Copyright (c) 2004-2005, Xilinx Inc.
	All rights reserved.
	
	Redistribution and use in source and binary forms, 
	with or without modification, are permitted provided 
	that the following conditions are met:
	- Redistributions of source code must retain the above 
	  copyright notice, this list of conditions and the 
	  following disclaimer.
	- Redistributions in binary form must reproduce the 
	  above copyright notice, this list of conditions and 
	  the following disclaimer in the documentation and/or 
	  other materials provided with the distribution.
	- Neither the name of the copyright holder nor the names 
	  of its contributors may be used to endorse or promote 
	  products derived from this software without specific 
	  prior written permission.
	
	THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND 
	CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, 
	INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF 
	MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE 
	DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR 
	CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, 
	SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT 
	NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; 
	LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) 
	HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN 
	CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR 
	OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS 
	SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
	
ENDCOPYRIGHT
*/
// Sequence.cal
//
// Author: David B. Parlour (dave.parlour@xilinx.com)
//

actor Addressing_simple () 
	int(size=12) BTYPE 
	==> 
	int(size=11) A, 
	int(size=11) B, 
	int(size=11) C :
	
	StartMode:
	contract BTYPE:3 ==>
		guard (BTYPE[@] & NEWVOP) != 0x000
	end
	
	IntraMode:
	contract BTYPE:1 ==> A:1, B:1, C:1
		guard (BTYPE[@] & NEWVOP) = 0x000
		guard (BTYPE[@] & INTRA) != 0x000
	end
	
	OtherMode:
	contract BTYPE:1 ==>
		guard (BTYPE[@] & NEWVOP) = 0x000
		guard (BTYPE[@] & INTRA) = 0x000
	end
	
	invariant rd@(BTYPE) = 0 ==> state(read)
	
	invariant mode(StartMode) && tot@(BTYPE) > 0 ==> (BTYPE[@] & NEWVOP) != 0x000
	invariant mode(IntraMode) && tot@(BTYPE) > 0 ==> (BTYPE[@] & NEWVOP) = 0x000 && (BTYPE[@] & INTRA) != 0x000
	invariant mode(OtherMode) && tot@(BTYPE) > 0 ==> (BTYPE[@] & NEWVOP) = 0x000 && (BTYPE[@] & INTRA) = 0x000
	
	invariant mode(StartMode) ==> tot@(A) = 0 && tot@(B) = 0 && tot@(C) = 0
	invariant mode(IntraMode) ==> tot@(A) <= 1 && tot@(B) <= 1 && tot@(C) <= 1
	invariant mode(OtherMode) ==> tot@(A) = 0 && tot@(B) = 0 && tot@(C) = 0
	
	invariant mode(StartMode) && rd@(BTYPE) = 1 ==> state(getw)
	invariant mode(StartMode) && rd@(BTYPE) = 2 ==> state(geth)
	invariant mode(StartMode) && rd@(BTYPE) = 3 ==> state(read)
	
	invariant mode(IntraMode) && state(predict) ==> rd@(BTYPE) = 1 && tot@(A) = 0 && tot@(B) = 0 && tot@(C) = 0
	invariant mode(IntraMode) && state(advance) ==> rd@(BTYPE) = 1 && tot@(A) = 1 && tot@(B) = 1 && tot@(C) = 1
	invariant mode(IntraMode) && state(read) ==> 
		(rd@(BTYPE) = 0 && tot@(A) = 0 && tot@(B) = 0 && tot@(C) = 0) || 
		(rd@(BTYPE) = 1 && tot@(A) = 1 && tot@(B) = 1 && tot@(C) = 1)
	
	invariant mode(StartMode) ==> state(read) || state(getw) || state(geth)
	invariant mode(IntraMode) ==> state(read) || state(predict) || state(advance)
	invariant mode(OtherMode) ==> state(read) || state(advance)
	
	// Generate indices into the stream history to locate
	//   A  neighbor to the left
	//   B  neighbor above left
	//   C  neighbor above
	//         comp layout
	//      Y         U       V
	//  +---+---+   +---+   +---+
	//  | 0 | 1 |   | 4 |   | 5 |
	//  +---+---+   +---+   +---+
	//  | 2 | 3 | 
	//  +---+---+
	
	int(size=8) mbx := int2bv(0,8);

	// Current macroblock x position
	bool top_edge := true;

	// Top edge macroblock flag
	bool left_edge;

	int(size=4) comp := int2bv(0,4);

	int(size=8) mbwidth := int2bv(0,8);

	// Macroblock width of current vop
	// Use 3 address lines for component index to save address calculation effort
	// Also, location 0 is reserved for all uncoded blocks. This protocol must
	// be identical to the buffer addressing in Reconstruct.cal, since pointer
	// values are passed on ports A, B, C.
	int(size=8) BUF_SIZE = int(121,8) + int(2,8);

	Map(int(size=11) -> bool, size=984) coded //:= [ false : for int i in 1 .. BUF_SIZE * 8 ];

	int(size=8) ptr;

	int(size=8) ptr_left;

	int(size=8) ptr_above;

	int(size=8) ptr_above_left;

	// buffer pointer wrap, skipping over address 0
	function decrement(int(size=8) p) --> int(size=8) :
		if p = int(1,8) then
			BUF_SIZE - int(1,8)
		else
			p - int(1,8)
		end
	end

	function access(int(size=8) p, int(size=4) c) --> int(size=11) :
	  bvresize(bvresize(p,11) << int(3,11),11) | bvresize(c,11)
	end

	// New VOP
	start: action BTYPE:[ cmd ] ==>
	guard
		(cmd & NEWVOP) != int2bv(0,12)
	do
		mbx := int2bv(0,8);
		top_edge := true;
		left_edge := true;
		comp := int2bv(0,4);
	end

	getw: action BTYPE:[ w ] ==>
	do
		mbwidth := bvresize(w,8);
		ptr := int2bv(1,8);
		ptr_left := int2bv(2,8);
		ptr_above := int2bv(1,8) + bvresize(w,8);
		ptr_above_left := int2bv(2,8) + bvresize(w,8);
	end

	geth: action BTYPE:[ h ] ==>
	end

	read.intra: action BTYPE:[ btype ] ==>
	guard
		(btype & INTRA) != int2bv(0,12)
	do
		coded[access(ptr, comp)] := true;
	end

	read.other: action BTYPE:[ btype ] ==>
	do
		coded[access(ptr, comp)] := false;
	end

	advance: action ==>
	do
		comp := comp + int2bv(1,4);
		if comp = int2bv(6,4) then
			comp := int2bv(0,4);
			mbx := mbx + int2bv(1,8);
			left_edge := false;
			if mbx = mbwidth then
				mbx := int2bv(0,8);
				top_edge := false;
				left_edge := true;
			end
			ptr := decrement(ptr);
			ptr_left := decrement(ptr_left);
			ptr_above := decrement(ptr_above);
			ptr_above_left := decrement(ptr_above_left);
		end
	end

	// Locate prediction indices for block 0
	predict: action ==> A:[ a ], B:[ b ], C:[ c ]
	var
		int(size=11) a, 
		int(size=11) b, 
		int(size=11) c
	do
		if comp = int2bv(0,4) then
		
			a := int2bv(0,11);  
			b := int2bv(0,11);  
			c := int2bv(0,11);
		
			if not left_edge then
			// left-neighbor is block 1 from previous macroblock
				a := access(ptr_left, int2bv(1,4));
				if not coded[a] then
					a := int2bv(0,11);
				end
				if not top_edge then
				// above-left-neighbor is block 3 from previous row, col macroblock
					b := access(ptr_above_left, int2bv(3,4));
					if not coded[b] then
						b := int2bv(0,11);
					end
				end
			end
			// above-neighbor is block 2 from previous row macroblock
			if not top_edge then
				c := access(ptr_above, int2bv(2,4));
				if not coded[c] then
					c := int2bv(0,11);
				end
			end
		end
		
		if comp = int2bv(1,4) then

			a := access(ptr, int2bv(0,4)); // left-neighbor is block 0 from this macroblock
			b := int2bv(0,11);
			c := int2bv(0,11);

			if not coded[a] then
				a := int2bv(0,11);
			end
			if not top_edge then
			// above-left-neighbor is block 2 from previous row macroblock
				b := access(ptr_above, int2bv(2,4));
				if not coded[b] then
					b := int2bv(0,11);
				end
				// above-neighbor is block 3 from previous row macroblock
				c := access(ptr_above, int2bv(3,4));
				if not coded[c] then
					c := int2bv(0,11);
				end
			end
		end
		
		if comp = int2bv(2,4) then
			// Locate prediction indices for block 2
			a := int2bv(0,11); 
			b := int2bv(0,11); 
			c := access(ptr, int2bv(0,4)); // above-neighbor is block 0 from this macroblock
	
			if not left_edge then
			// left-neighbor is block 3 from previous macroblock
				a := access(ptr_left, int2bv(3,4));
				if not coded[a] then
					a := int2bv(0,11);
				end
				// above-left-neighbor is block 1 from previous macroblock
				b := access(ptr_left, int2bv(1,4));
				if not coded[b] then
					b := int2bv(0,11);
				end
			end
			if not coded[c] then
				c := int2bv(0,11)
			end
		end
		
		if comp = int2bv(3,4) then
		
			a := access(ptr, int2bv(2,4)); // left-neighbor is block 2 from this macroblock
			b := access(ptr, int2bv(0,4)); // above-left-neighbor is block 0 from this macroblock
			c := access(ptr, int2bv(1,4)); // above-neighbor is block 1 from this macroblock
		
			if not coded[a] then
				a := int2bv(0,11);
			end
			if not coded[b] then
				b := int2bv(0,11);
			end
			if not coded[c] then
				c := int2bv(0,11);
			end
		end
		
		if comp = int2bv(4,4) or comp = int2bv(5,4) then
		
			a := int2bv(0,11); 
			b := int2bv(0,11); 
			c := int2bv(0,11);
		
			if not left_edge then
			// left-neighbor is from previous macroblock
				// Note: just set curr and requested comp to 0 since they offset
				a := access(ptr_left, comp);
				if not coded[a] then
					a := int2bv(0,11);
				end
				if not top_edge then
				// above-left-neighbor is from previous row, col macroblock
					// Note: just set curr and requested comp to 0 since they offset
					b := access(ptr_above_left, comp);
					if not coded[b] then
						b := int2bv(0,11);
					end
				end
			end
			// above-neighbor is from previous row macroblock
			// Note: just set curr and requested comp to 0 since they offset
			if not top_edge then
				c := access(ptr_above, comp);
				if not coded[c] then
					c := int2bv(0,11);
				end
			end
		end
	end

	schedule fsm read :
		read ( start ) --> getw;
		getw ( getw ) --> geth;
		geth ( geth ) --> read;
		read ( read.intra ) --> predict;
		read ( read.other ) --> advance;
		predict ( predict ) --> advance;
		advance ( advance ) --> read;
	end

	priority
		start > read.intra;
		start > read.other;
		read.intra > read.other;
	end

end