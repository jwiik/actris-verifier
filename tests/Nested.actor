@static
actor Add int in1, int in2 ==> int out :
  action in1:[i], in2:[j] ==> out:[i+j] end
end

@static
actor Split int in1 ==> int out1, int out2 :
  action in1:[i] ==> out1:[i], out2:[i] end
end

@static
actor Delay(int k) int in1 ==> int out :
  initialize ==> out:[k] end
  action in1:[i] ==> out:[i] end
end

network SumNet int in1 ==> int out :
  
  Main: contract in1:1 ==> out:1
    requires 0 <= in1[@]
    ensures out[0] = in1[0]
    ensures out[@] >= in1[@]
    ensures 0 < @(out) ==> out[@] = out[@-1]+in1[@]
  end
  
  contract invariant tokens(sn_b,1)
  stream contract invariant rd(in1) = tot(out)
  stream contract invariant tot(out) > 0 ==> out[0] = in1[0]
  stream contract invariant (forall int i :: every(out,i,1,0) ==> out[i] = out[i-1]+in1[i])
  
  chinvariant sn_b[0] = 0
  chinvariant 0 <= sn_b[@]
  
  entities
    add = Add();
    del = Delay(0);
    spl = Split();
  end
  
  structure
    sn_a: in1 --> add.in1;
    sn_b: del.out --> add.in2;
    sn_c: add.out --> spl.in1;
    sn_d: spl.out1 --> out;
    sn_e: spl.out2 --> del.in1;
  end

end

actor Sum int x ==> int y: 

  int sum;
  
  invariant 0 <= sum
  invariant rd(x) > 0 ==> sum = y[last]
  invariant tot(y) = 0 ==> sum = 0
  
  stream action invariant rd(x) = tot(y)
  stream action invariant tot(y) > 0 ==> y[0] = x[0]
  stream action invariant (forall int i :: every(y,i,1,0) ==> y[i] = y[i-1]+x[i])

  initialize ==> do
    sum := 0;
  end
  
  action x:[i] ==> y:[sum]
    requires 0 <= i
    ensures i <= sum 
  do
    sum := sum+i;
  end 
  
end

network Nested int in1 ==> int out1, int out2 :
  
  Main: contract in1:1 ==> out1:1, out2:1
    requires 0 <= in1[@]
    ensures 0 < @(out1) ==> out1[@] = out1[@-1]+in1[@]
    ensures 0 < @(out2) ==> out2[@] = out2[@-1]+in1[@]
    ensures out1[@] = out2[@]
  end
  
  action invariant tot(d) = rd(b)
  action invariant tot(d) > 0 ==> d[0] = b[0]
  action invariant (forall int k :: every(d,k,1,0) ==> d[k] = d[k-1]+b[k])
  action invariant (forall int k :: history(out1,k) ==> out1[k] = out2[k])
  
  entities
    net = SumNet();
    sum = Sum();
    spl = Split();
  end
  
  structure
    a: in1 --> spl.in1;
    b: spl.out1 --> net.in1;
    c: spl.out2 --> sum.x;
    d: net.out --> out1;
    e: sum.y --> out2;
  end

end
