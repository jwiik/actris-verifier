type Int(int i)

actor Adjudicator Int x1, Int x2 ==> Int y :

  stream invariant tot(y) = rd(x2)
  stream invariant rd(x1) <= rd(x2)
  
  invariant rd(x2) = c
  invariant (forall int i :: 0 <= i && i < rd(x2) ==> x2[i].sqn = i) 
  invariant (forall int i :: 0 <= i && i < tot(y) ==> y[i].sqn = i)
      
  int c;

  normal: action x1:[i], x2:[j] ==> y:[i]
    guard i.sqn = j.sqn && i.sqn = c
    do c := c+1;
  end
  
  timeout: action x2:[j] ==> y:[j] 
    guard j.sqn = c
    do c := c+1;
  end
  
  readoff: action x1:[i] ==>
    guard i.sqn < c
    requires rd(x1) < rd(x2)
  end
  
  initialize ==>
    do c := 0;
  end
  
  priority 
    readoff > normal;
    readoff > timeout;
    timeout > normal
  end
  
end